

void main() {
    
    print "----------------BRAINFUCK-INTERPRETER----------------";
    while (true) {
        print "";
        print "Enter program: ";

        str program;
        input program;

        int[] state = [256;];
        int ptr = 0;

        for (int i = 0; i < program.length; i += 1; ) {
            char ch = program[i];
            if (ch == '+') {
                state[ptr] += 1;
                if (state[ptr] > 255) state[ptr] = 0;
            }
            else if (ch == '-') {
                state[ptr] -= 1;
                if (state[ptr] < 0) state[ptr] = 255;
            }
            else if (ch == '>') {
                ptr += 1;
                if (ptr > 255) ptr = 0;
            }
            else if (ch == '<') {
                ptr -= 1;
                if (ptr < 0) ptr = 255;
            }
            else if (ch == '.') write (char)state[ptr];
            else if (ch == ',') {
                int inp;
                input inp;
                state[ptr] = inp;
            } else if (ch == '[') {
                if (state[ptr] == 0) {
                    int depth = 1;
                    while (depth > 0) {
                        i += 1;
                        if (program[i] == '[') {
                            depth += 1;
                        }
                        else if (program[i] == ']') {
                            depth -= 1;
                        }
                    }
                }
            } else if (ch == ']') {
                if (state[ptr] != 0) {
                    int depth = 1;
                    while (depth > 0) {
                        i -= 1;
                        if (program[i] == '[') depth -= 1;
                        else if (program[i] == ']') depth += 1;
                    }
                }
            }

        }

        print "";
        print "Done :D!";
    }

}













main();